plugins {
    id 'java'
}

group = 'com.pourbowy'
version = '1.0.0'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
    options.release = 8
}

def stubs = sourceSets.create('stubs') {
    java.srcDir 'src/stubs/java'
    resources.srcDirs = []
}

tasks.named('compileJava') {
    dependsOn(tasks.named(stubs.compileJavaTaskName))
    classpath += stubs.output
}

tasks.named(stubs.compileJavaTaskName) {
    options.release = 8
}

repositories {
    mavenCentral()
}

tasks.jar {
    archiveBaseName = 'pourbowy'
}

tasks.processResources {
    inputs.property('version', project.version)
    inputs.property('mcversion', '1.6.4')

    filesMatching('mcmod.info') {
        expand('version': project.version, 'mcversion': '1.6.4')
    }
}
